Alphabet

   А Б В Г Д Е Ё Ж З И Й К Л М Н О Ө П Р С Т У Ү Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я Һ Ҥ Ҕ
   а б в г д е ё ж з и й к л м н о ө п р с т у ү ф х ц ч ш щ ъ ы ь э ю я һ ҥ ҕ

   %{E%}:а %{E%}:э %{E%}:ө %{E%}:о

   !%{A%}:а %{A%}:э %{A%}:ө %{A%}:о
   %{A%}:а
   !%{I%}:и %{I%}:у %{I%}:ү %{I%}:ы
   %{I%}:ы

   %{L%}:л %{L%}:т %{L%}:д %{L%}:н
   %{T%}:т ! %{T%}:д %{T%}:л %{T%}:н

   %{B%}:б ! %{B%}:п %{B%}:м

   %{G%}:г %{G%}:к %{G%}:ҕ %{G%}:х %{G%}:ҥ

   %{i%}:0 ! %{i%}:и %{i%}:у %{i%}:ү %{i%}:ы

   %{n%}:0 ! %{n%}:н

   %{t%}:0 ! %{t%}:т

   %{a%}:0

   %{р%}:р

   %>:0 ; 

Sets

Vow = Я Ю Ё А О Ы У Э Е И Ө Ү
      я ю ё а о ы у э е и ө ү ;

Nasal = М Н Ҥ
        м н ҥ ;

VoicelessCns = П Т К Х С 
               п т к х с ;

Obs = Б В Г Д Ж З К П Р С Т Ф Х Ц Ч Ш Щ Һ Ҕ
      б в г д ж з к п р с т ф х ц ч ш щ һ ҕ ;

Cons = Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц Ч Ш Щ Һ Ҥ Ҕ
       б в г д ж з й к л м н п р с т ф х ц ч ш щ һ ҥ ҕ ;


Rules

!"Vowel deletion"
!%{A%}:0 <=> _ %>: %{I%}: ; 


!!!== regressive assimilation ==!!!

"т assimilation"
т:Cy <=> _ %>: Cx: ;
       where Cx in ( %{B%} %{G%} ) ! %{L%} ) 
             Cy in (   п     к   ) !  т ) 
       matched ;  


!!!== progressive assimilation ==!!!

"{B} devoicing"
%{B%}:п <=> :VoicelessCns/:0 _ ;

"{B} nasalisation"
%{B%}:м <=> :Nasal/:0 _ ;

"{T} → л"
%{T%}:л <=> :л/:0 _ ; 


!!!== deletion ==!!!

"{р} deletes before obstruents or something"
%{р%}:0 <=> _ :Obs/:0 ;


!!!== assimilation ==!!!

"{р} → л before л"
%{р%}:л <=> _ [ :л | [ %{L%}: - :Obs ] ]/:0 ;


!!!== epenthesis ==!!!

"т after a vowel"
%{t%}:т <=> :Vow/:0 _ ;

"н after a vowel"
%{n%}:н <=> :Vow/:0 _ ;


!!!== lenition ==!!!

"х → ҕ intervocalically"
х:ҕ <=> _ %>: :Vow/:0* ;

"с → һ intervocalically"
с:һ <=> _ %>: :Vow/:0* ;


!!!== Vowel Harmony ==!!!

"vowel harmony for {i}"
%{i%}:Vy <=> :Vx [ :Cons* :Cons ]/:0* _ ;
         where Vx in ( а о ы у э е и ө ү )
               Vy in ( ы у ы у и и и ү ү )
         matched ;

"vowel harmony for {I}"
%{I%}:Vy <=> :Vx [ :Cons* :Cons ]/:0* _ ;
         where Vx in ( а о ы у э е и ө ү )
               Vy in ( ы у ы у и и и ү ү )
         matched ;

"vowel harmony for {A} except after уо and үө"
%{A%}:Vy <=> :Vx [ :Cons* :Cons ]/:0* _ ;
         except
             !:ү :ө [ :Cons* :Cons]/:0* _ ;
             :у :о [ :Cons* :Cons]/:0* _ ;
         where Vx in ( а о ы у э е и ө ү )
               Vy in ( а о а а э э э ө э )
         matched ;

"vowel harmony for {A} after уо"
! shouldn't need a rule because it's the default а, but doesn't seem to work without one
%{A%}:а <=> :у :о [ :Cons* :Cons ]/:0* _ ;

!"vowel harmony for {A} after үө"
!%{A%}:э <=> :ү :ө [ :Cons* :Cons ]/:0* _ ;

"vowel harmony for {a} except after уо and үө"
%{a%}:Vy <=> :Vx [ :Cons* :Cons ]/:0* _ ;
         except
             !:ү :ө [ :Cons* :Cons]/:0* _ ;
             :у :о [ :Cons* :Cons]/:0* _ ;
         where Vx in ( а о ы у э е и ө ү )
               Vy in ( а о а а э э э ө э )
         matched ;

"vowel harmony for {a} after уо"
! shouldn't need a rule because it's the default а, but doesn't seem to work without one
%{a%}:а <=> :у :о [ :Cons* :Cons ]/:0* _ ;

"vowel harmony for {a} after үө"
%{a%}:э <=> :ү :ө [ :Cons* :Cons ]/:0* _ ;

