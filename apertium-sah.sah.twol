Alphabet

   А Б В Г Д Е Ё Ж З И Й К Л М Н О Ө П Р С Т У Ү Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я Һ Ҥ Ҕ
   а б в г д е ё ж з и й к л м н о ө п р с т у ү ф х ц ч ш щ ъ ы ь э ю я һ ҥ ҕ

   !%{E%}:а %{E%}:э %{E%}:ө %{E%}:о

   %{A%}:а %{A%}:э %{A%}:ө %{A%}:о %{A%}:0  ! 0 before {I}{I} etc.
   !%{A%}:а
   %{I%}:и %{I%}:у %{I%}:ү %{I%}:ы %{I%}:0
   !%{I%}:ы

   %{L%}:л ! %{L%}:т %{L%}:д %{L%}:н
   %{T%}:т %{T%}:д %{T%}:л %{T%}:н

   %{B%}:б ! %{B%}:п %{B%}:м

   %{G%}:г %{G%}:к %{G%}:ҕ %{G%}:х %{G%}:ҥ

   %{i%}:0 ! %{i%}:и %{i%}:у %{i%}:ү %{i%}:ы

   %{ы%}:0 !  %{ы%}:и %{ы%}:у %{ы%}:ү %{ы%}:ы

   %{y%}:0 ! %{y%}:и %{y%}:у %{y%}:ү %{y%}:ы

   %{n%}:0 %{n%}:н

   %{t%}:0 ! %{t%}:т

   %{a%}:0

	%{H%}:һ ! %{H%}:и %{H%}:у %{H%}:ү %{H%}:ы

   %{Н%}:н ! %{н%}:0
   
	%{m%}:0 ! %{m%}:м
	
	%{О%}:о ! unstressed Russian о that doesn't trigger rounding harmony

   %{р%}:р
   %{Р%}:р %{Р%}:л

   %{☭%}:0
   %{ъ%}:0

   %{рл%}:р ! р except before л, then л

   %{D%}:т %{D%}:д %{D%}:0 ! д before a vowel, otherwise т

   %{н%}:н ! Reflexive н that deletes before passive
   %{т%}:т ! Derivational т that deletes before passive
   %{лл%}:л ! Derivational т that deletes before passive
   %{й%}:й ! Verb-stem-final й that deletes before passive
	%{ь%}:0 ! disappearing ь

	%{а%}:0
	%{э%}:0
	%{ө%}:0
	%{ү%}:0
	%{о%}:0
	%{у%}:0
	%{и%}:0
	%{м%}:0
	%{с%}:0
	%{х%}:0
	%{d%}:0
	%{йр%}:0 ! imaginary й or р
	%{л%}:0 ! imaginary л
	%{Noun%}:0 ! for noun-specific phonology
   %{❗%}:0 ! Weird exceptions

   %-:%-

   %>:0 ; 


Sets

Vow = Я Ю Ё А О Ы У Э Е И Ө Ү
      я ю ё а о ы у э е и ө ү ;

RealVow = А О Ы У Э И Ө Ү
          а о ы у э и ө ү ;

RoundVow = О У Ө Ү
           о у ө ү ;

UnRoundVow = А Ы Э И
             а ы э и ;

LowVow = Я Ё А О Э Е Ө
         я ё а о э е ө ;

HighVow = Ю Ы У И Ү
          ю ы у и ү ;

Nasal = М Н Ҥ
        м н ҥ ;

HighSonCns = Й Р
             й р ;

VoicelessCns = П Д Т К Х С З Ж В
               п д т к х с з ж в ;

Obs = Б В Г Д Ж З К П Р С Т Ф Х Ц Ч Ш Щ Һ Ҕ
      б в г д ж з к п р с т ф х ц ч ш щ һ ҕ ;

Cons = Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц Ч Ш Щ Һ Ҥ Ҕ Ь
       б в г д ж з й к л м н п р с т ф х ц ч ш щ һ ҥ ҕ ь ;

Imaginary = %{а%} %{м%} %{с%} %{d%} %{йр%} %{л%} %{э%} %{о%} %{у%} %{ө%} %{ү%} %{и%} ;

ImaginaryVoicelessCns = %{х%} %{с%} %{d%} ;

ImaginaryVow = %{а%} %{э%} %{о%} %{у%} %{ө%} %{ү%} %{и%} ;

ImaginaryCons = %{м%} %{с%} %{йр%} %{л%} %{d%} ;

ImaginaryHighVow = %{у%} %{ү%} %{и%} ;

ImaginaryLowVow = %{а%} %{э%} %{о%} %{ө%} ;

Rules

!"Vowel deletion"
!%{A%}:0 <=> _ %>: %{I%}: ; 


!!!== regressive assimilation ==!!!

"т assimilation"
т:Cy <=> _ %>: Cx: ;
     except
         [ :н | :с | :к ] _ %>:/[ :0 - %>: ] ;
     where Cx in ( %{B%} %{G%} ) ! %{L%} ) 
           Cy in (   п     к   ) !  т ) 
     matched ;  

"{р} → л before л"
%{р%}:л <=> _ [ :л | [ %{L%}: - :Obs ] ]/:0 ;

"{рл} → л before л and other random contexts"
%{рл%}:л <=> _ [ :л | [ %{L%}: - :Obs ] ]/:0 ;
             _ %{y%}:0 ;
!@ с ы {рл}:л {y}:0 т:д {ь}:ь >:0 {A}:а {р}:р

"{Р} as л before л"
%{Р%}:л <=> _ [ :л | [ %{L%}: - :Obs ] ]/:0 ;


! FIXME: These two rules should probably be generalised
"н→ҥ assimilation"
Cx:ҥ <=> _ :ҥ/:0 ; 
     except
         _ (%{ы%}:) %>: %{i%}: :л ;
     where Cx in ( н %{н%} ) ;

"н→м assimilation"
Cx:м <=> _ (%{☭%}:) %>: %{B%}: ; 
     where Cx in ( н %{н%} ) ;

"н→л assimilation for verbs"
Cx:л <=> :л %{y%}:0 _ [ :Vow ]/[:0 - %{Noun%}:]* ;
     except
         _ (%{ы%}:) %>: %{i%}: :л ;
     where Cx in ( н %{н%} ) ;

"л→н assimilation for nouns"
л:н <=> _ %{y%}:0 [ н: - :0 ] %{Noun%}: [ :Vow ]/[:0 - %{Noun%}:]* ;

"н/л→т desonorisation"
Cx:т <=> :VoicelessCns %{y%}:0 _ :Vow/:0* ;
     where Cx in ( н л ) ;
!@ у с {y}:0 н:т >:0 {A}:а {р}:р
!@ у с {y}:0 л:т >:0 {A}:а {р}:р
"%{н%}→т desonorisation"
%{н%} <=> :VoicelessCns %{y%}:0 _ :Vow/:0* ;
      except
          _ (%{ы%}:) %>: %{i%}: :л ;
!@ а х {y}:0 н:т >:0 {i}:ы л {y}:ы н {ы}:0

"н→д desonorisation"
н:д <=> :HighSonCns %{y%}:0 _ :Vow/:0* ;
!@ с а й {y}:0 н:д >:0 {A}:а {р}:р

"т→д voicing"
т:д <=> [ :HighSonCns | :Nasal ] %{y%}:0 _ [ :Vow - %{ь%}: ]/[:0 - %{ь%}:]* ;
!        [ :HighSonCns ] _ %{☭%}: %>: (:0*) :Vow ;
    except
        _ :* %{❗%}: ;

!@ с ы {рл}:л {y}:0 т:д {ь}:ь >:0 {A}:а {р}:р
! but not here:
!@ и с:һ {у}:0 т >:0 {A}:а {р}:л >:0 {L}:л {A}:а р

!@ с ы {рл}:л {y}:0 т:д {ь}:ь >:0 {I}:ы м >:0 {A}:а {A}:а т

!@ с п о р т:д {☭}:0 >:0 {n}:0 {I}:у

"р→т assimilation"
р:т <=> _ %{y%}:0 т: %{❗%}: ;

"р→н assimilation"
р:н <=> _ %{y%}:0 н: ;

! Having :Vow/:0* at the end of the above rules
! ensures that they don't conflict with н→ҥ and н→м rules
! ... even though it's more specific than it really needs to be.

!"{D} to т before consonant or end of word"
!%{D%}:т <=> _ :0* :Cons ;
!            _ :0* .#. ;
"{D} as д before a vowel"
%{D%}:д <=> _ [ :0 - [ %{T%}: | %{G%}: ] ]* :Vow ;

"{D} deletes before certain archiphonemes"
%{D%}:0 <=> _ [ :0 - Imaginary: ]* [ %{G%}: | %{T%}: ] ;

! {T} excluded because of т а р {D}:т > {T}:0 > {A}:а

"и{й} to ээ before һин: {й}"
%{й%}:э <=> и: _ %>: %{H%}: ;

"и{й} to ээ before һин: э"
и:э <=> _ %{й%}: %>: %{H%}: ;



!!!== progressive assimilation ==!!!

"Assimilation after nasals"
Cx:Cy <=> :Nasal [ [ :0 - [ т: | Imaginary: | %{D%}: ] ] | %-: ]* _ ;
          %{м%}: [ [ :0 - [ Imaginary: | %{D%}: ] ] | %-: ]* _ ;
      where Cx in ( %{L%} %{T%} %{G%} %{B%} )
            Cy in (   н     н     ҥ     м   )
      matched ;

"Assimilation after high-sonority obstruents"
Cx:Cy <=> :HighSonCns [ [ :0 - [ Imaginary: | %{D%}: ] ] | %-: | :ь ]* _ ;
          %{йр%}: [ [ :0 - [ ImaginaryCons: | %{D%}: ] ] | %-: | :ь ]* _ ;
      except
!          %{р%}:/:0* _ ;          ! not necessary apparently
          %{Р%}:/:0* _ ;

      where Cx in ( %{L%} %{T%} )
            Cy in (   д     д   )
      matched ;

"Assimilation after voiceless consonants"
Cx:Cy <=> :VoicelessCns [ [ :0 - Imaginary: ] | %-: | ь: ]* _ ;
          %{с%}: [ [ :0 - Imaginary: ] | %-: ]* _ ;
      where Cx in ( %{L%} %{B%} )
            Cy in (   т     п   )
      matched ;

"{T} → л"
%{T%}:л <=> [ :л | %{л%}: ] [ [ :0 - Imaginary: ] | %-: ]* _ ; 

"{G} after х"
%{G%}:х <=> :х [ [ :0 - [ Imaginary: | %{D%}:0 ] ] | %-: ]*  _ ;

"{G} after voiceless consonants besides х"
%{G%}:к <=> [ [ :VoicelessCns - :х ] | [ ImaginaryVoicelessCns: - %{х%}: ] ] [ [ :0 - [ Imaginary: | %{D%}: ] ] | %-: ]* _ ;
            %{D%}:0 [ :0 - [ Imaginary: | %{D%}: ] ]* _ ;

"some consonants turn to д for дь stuff"
Cx:д <=> %{рл%}:л %{y%}:0 _ %{ь%}: ;
     where Cx in ( й т ) ;


!!!== deletion ==!!!

"{р} deletes before obstruents or something"
%{р%}:0 <=> _ :Obs/:0 ;


"contexts for {й} as 0"
%{й%}:0 <=> _ %>: %{i%}: :л ;  ! at end of stem before passive

"contexts for {i} as 0"
%{i%}:0 <=> :Vow/[ :0 - ImaginaryCons: ]* _ ; ! after vowels!

"{н}, {лл}, {т} delete at end of stem before passive"
Cx:0 <=> _ (%{ы%}:) %>: %{i%}: :л ;
     where Cx in ( %{н%} %{т%} %{лл%} ) ;

"{T} deletes after clusters ending in т"
%{T%}:0 <=> :р [ :т | %{D%}: ] %>: _ ;
                %{d%}: ( :%- ) %>: _ ;

"{Н} deletes if prev {H} isn't h"
%{Н%}:0 <=> [ %{H%}: - :һ ] %{I%}: _ ;

"vowels delete before {A}{A}"
Vx:0 <=> _ %>: %{A%}: %{A%}: ;
         _ RealVow: %>: %{A%}: %{A%}: ;
     except
         .#. :Cons (:ь) _ ;
         .#. :Cons (:ь) RealVow: _ ;
         .#. RealVow: _ ;
         .#. _ ;
     where Vx in RealVow ;

!@ с ы л д ь ы:0 >:0 {A}:а {A}:а ч ч {I}:ы

!"some stem-final consonants delete in passive after {y}"
!Cx:0 <= %{y%}: _ (%{ы%}:) %>: %{i%}: л: %{y%}: н: ;
!     where Cx in ( т л н ) ;

!XXX и с:һ {y}:0 т:0 >:0 {i}:и л {y}:и н {ы}:0
!@ и с:һ {y}:0 {т}:0 >:0 {i}:и л {y}:и н {ы}:0

"shorter passive when consonant deletes"
н:0 <=> [ %{н%}:0 | %{т%}:0 | %{й%}:0 | %{лл%}:0 ]  (%{ы%}:) %>: %{i%}: л: %{y%}: _ %{ы%}: %>: ;
    except
        _ :ҥ/:0 ;  ! avoid conflict for н→ҥ assimilation
        :л %{y%}:0 _ [ :Vow ]/[:0 - %{Noun%}:]* ; ! avoid conflict for н→л assimilation
        :VoicelessCns %{y%}:0 _ :Vow/:0* ; ! avoid conflict for н/л→т desonorisation

!!!== epenthesis ==!!!

"т after a vowel"
%{t%}:т <=> :Vow/:0 _ ;

!@ у с т >:0 {t}:0 {A}:а

"н after a vowel"
%{n%}:н <=> [ :Vow | ImaginaryVow: ]/[ [ :0 - Imaginary: ] | %-: ] _ ;
            !@ и д е я >:0 {n}:н {I}:ы
            !@ 2 {э}:0 - >:0 {n}:н {I}:и

"н after a cons"
%{n%}:0 <=> [ :Cons | ImaginaryCons: ]/[ [ :0 - Imaginary: ] | %-: ] _ ;

"disappearing ь appears when part of дь"
!!%{ь%}:ь <=> [ %{y%}:0 :Cons* :Cons ]/[ :0 - %{y%}: ]* _ ;
!!%{ь%}:ь <=> %{y%}: :д _ ;
%{ь%}:ь <=> [ \д ]:д _ ;

!@ с ы {рл}:л {y}:0 т:д {ь}:ь >:0 {A}:а {р}:р

"м after some stems"
%{m%}:м <=> ы: а: %>: _ ;

!!!== lenition ==!!!

"intervocalic lenition"
Cx:Cy <=> :Vow/:0* _ [ %>: :Vow]/[ :0 - %>: ]* ;
             !@ и с:һ {y}:и т
          ! :Vow/:0* _ :0* %>: :0* :Vow ;
          !    !е к:__ т:0 {☭}:0 >:0 {t}:0 {A}:а
          :Vow _ %{y%}:Vow ; 
      where Cx in ( х с к п )
            Cy in ( ҕ һ г б )
      matched ;


"{G} after low vowel"
%{G%}:ҕ <=> [ :LowVow | ImaginaryLowVow: ] [ [ :0 - [ Imaginary: | %{D%}: ] ] | %-: ]* _ ;
            !@ Е в р о п а >:0 {G}:ҕ {A}:а


!!!== Vowel Harmony ==!!!

"{I} vowel harmony after imaginary stuff"
%{I%}:Vy <= Vx: [ :Cons | [ :0 - ImaginaryVow: ] | :%- ]* _ ;
         except
             %{A%}:0 %>: _ [ %{I%}: | %{A%}: ] ;
         where Vx in ( %{а%} %{э%} %{ө%} %{о%} %{у%} %{ү%} %{и%} )
               Vy in (   ы     и     ү     у     у     ү     и   )
         matched ;

"{A} vowel harmony after imaginary stuff"
%{A%}:Vy <=  Vx: [ :Cons | [ :0 - ImaginaryVow: ] | :%- ]* _ ;
         except
             _ %>: %{I%}: [ %{I%}: | %{A%}: ]  ;
         where Vx in ( %{а%} %{э%} %{ө%} %{о%} %{у%} %{ү%} %{и%} )
               Vy in (   а     э     ө     о     а     э     э   )
         matched ;

"{i} vowel harmony after imaginary staff"
%{i%}:Vy <= Vx: :Cons [ [ :0 - ImaginaryVow: ] | :%- ]* _ ;
         except
            _ %>: %{A%}: ;
         where Vx in ( %{а%} %{э%} %{ө%} %{о%} %{у%} %{ү%} %{и%} )
               Vy in (   ы     и     ү     у     у     ү     и   )
         matched ;

"vowel harmony for {i}"
%{i%}:Vy <= :Vx [ :Cons* :Cons ]/[ :0 - [ %{☭%}: | ImaginaryVow: ] ]* _ ;
         except 
             _ %>: %{A%}: ; ! because {i}:0 >:0 {A}: 
                !@ э:э {T}:т {i}:0 >:0 {A}:э
         where Vx in ( а о ы у э е и ө ү я )
               Vy in ( ы у ы у и и и ү ү ы )
         matched ;

"vowel harmony for {y}"
%{y%}:Vy <=> :Vx [ :Cons* :Cons ]/:0* _ ;
         except 
             _ [ :Cons :Vow ]/[ :0 | :ь ]* ;
             !@ ис{y}т
             _ [ %{н%}:0 | %{т%}:0 | %{лл%}:0 ] (%{ы%}:) %>: %{i%}: ;
         where Vx in ( а о ы у э е и ө ү я )
               Vy in ( ы у ы у и и и ү ү ы )
         matched ;


"vowel harmony for {ы}"
%{ы%}:Vy <=> :Vx [ :Cons* :Cons ]/:0* _ %>: %{B%}: %{I%}: т: ;
             :Vx [ :Cons* :Cons ]/:0* _ %>: %{B%}: %{A%}: т: ;
         where Vx in ( а о ы у э е и ө ү я )
               Vy in ( ы у ы у и и и ү ү ы )
         matched ;

! оҥоруллубута
! табыллыбат


"vowel harmony for {I}"
%{I%}:Vy <=  [ :Vx - %{О%}: ] [ :0 - [ %{☭%}: | ImaginaryVow: ] ]* [ :Cons* :Cons ]/[ :0 - [ %{☭%}: | ImaginaryVow: | %{A%}: ] ]* _ ;
             %{I%}:Vx _ ;
             %{H%}:Vx _ ;
             :Vx [ :Cons* :Cons ]/[ :0 - [ %{☭%}: | ImaginaryVow: | %{A%}: ] ]* %{A%}:0 (%>:) _ [ %{I%}: | %{A%}: ]  ;
!@ д и э >:0 {A}:0 {A}:0 р {I}:и
!@ б а р >:0 {i}:ы м {A}:0 >:0 {I}:ы {I}:ы м
         !except
         !    ! [ %{ъ%}: | %{☭%}: ]  %>: [ :Cons* ]/:0* _ ;
         !    %{☭%}: [ :Cons* :Cons ]/:0* _ ;
         except
              :RealVow :RealVow %>: %{I%}: _ ;
         where Vx in ( а о ы у э е и ө ү я )
               Vy in ( ы у ы у и и и ү ү ы )
         matched ;

"vowel harmony after {О}"
Vx:Vy <=  %{О%}: [ :Cons* :Cons ]/[ [ :0 - [ %{☭%}: | ImaginaryVow: | %{A%}: | %{I%}: ] ] | :ь ]* _ ;
      except
          ! following exceptions needed to avoid conflicts with {A} harmony
          :ү :ө [ :Cons* :Cons]/:0* _ ;
          :у :о [ :Cons* :Cons]/:0* _ ;
          %{☭%}: [ :Cons* :Cons ]/:0* _ ;
          .#. ( :Cons (:ь) ) RealVow: RealVow: %>: (%{A%}:) _ ;
          _ %>: %{I%}: [ %{I%}: | %{A%}: ]  ;
      where Vx in ( %{A%} %{I%} )
            Vy in (   а     ы   )
      matched ;

"vowel harmony for {H}"
%{H%}:Vy <=> :Vx [ :Cons* :Cons ]/[ :0 - %{☭%}: ]* _ ;
         where Vx in ( а о ы у э е и ө ү я )
               Vy in ( ы у ы у и и и ү ү ы )
         matched ;


!!FIXME! ф а к у л ь т е т {☭}:0 >:0 {n}: {I}:

!@ б у о л >:0 {I}:у {A}:а х >:0 т {A}:а {A}:а х

"vowel harmony for {A} except after уо and үө"
%{A%}:Vy <=  [ :Vx - %{О%}: ] [ :Cons* :Cons ]/[ [ :0 - [ %{☭%}: | ImaginaryVow: ] ] | :ь ]* _ ;
             ! :Cons :Vx _ ;  ! FIXME: When does this happen?
             %{A%}:Vx _ ;
         except
             :ү :ө [ :Cons* :Cons]/:0* _ ;
             :у :о [ :Cons* :Cons]/:0* _ ;
             ! :Vow :Vow %>: _ ; 
             !    ! for verb stems ending in long vowel or diphthong
             !%{ъ%}: %>: [ :Cons* ]/:0* _ ; 
             %{☭%}: [ :Cons* :Cons ]/:0* _ ;
             .#. ( :Cons (:ь) ) RealVow: RealVow: %>: (%{A%}:) _ ;
				 _ %>: %{I%}: [ %{I%}: | %{A%}: ]  ;
         where Vx in ( а о ы у э е и ө ү я )
               Vy in ( а о а а э э э ө э а )
         matched ;

"{A} as part of {I}{A}"
%{A%}:Vy <=  %{I%}:Vx _ ;
         except
             :RealVow :RealVow %>: %{I%}:Vx _ ;
             _ %>: %{I%}: [ %{I%}: | %{A%}: ] ;   ! FIXME: figure out what to do in this context at some point
         where Vx in ( и ү ы у )
               Vy in ( э ө а о )
         matched ;

"{A} deletes after long vowels and diphthongs and triggers other stuff (and as part of {I}{A})"
%{A%}:0 <=> :RealVow :RealVow %>: (%{I%}:) _ ;
            .#. ( :Cons (:ь) ) RealVow: RealVow: %>: (%{A%}:) _ ;
            _ %>: %{I%}: [ %{I%}: | %{A%}: ]  ;  ! "{A} as nothing before {I}{I} and {I}{A}"

"{I} deletes after long vowels and diphthongs and triggers other stuff"
%{I%}:0 <=> :RealVow :RealVow %>: _ ;
            :RealVow :RealVow %>: %{I%}: _ ;

"first element of diphthongs and long vowels immediately before ({I}){A} and after unrounded vowels"
Vx:Vy <=> :UnRoundVow/[ :Cons | :0 ]* _ :RealVow %>: (%{I%}:) %{A%}: ;
      except
          _ RealVow: %>: %{A%}: %{A%}: ;
      where Vx in ( а о э ө )
            Vy in ( ы у и ү )
      matched ;

"first element of diphthongs and long vowels immediately before ({I}){A} and after rounded vowels"
Vx:Vy <=> :RoundVow/[ :Cons | :0 ]* _ :RealVow %>: (%{I%}:) %{A%}: ;
      except
          _ RealVow: %>: %{A%}: %{A%}: ;
      where Vx in ( а о э ө )
            Vy in ( у у ү ү )
      matched ;

"first element of diphthongs and long vowels immediately before {m}{i}м"
Vx:Vy <=> _ LowVow: %>: %{m%}: %{i%}: м: ;
      where Vx in ( а о э ө )
            Vy in ( ы у и ү )
      matched ;

!@ а һ а:ы а >:0 {i}:0 м >:0 {I}:ы {A}:а н

!"first element of diphthongs and long vowels immediately before {I}{A}"
!Vx:Vy <=> _ :RealVow %>: %{I%}: %{A%}: ;
!      where Vx in ( э а о ө )
!            Vy in ( и ы у ү )
!      matched ;

"second element of diphthongs and long vowels immediately before {A} unround"
Vx:Vy <=> :UnRoundVow _ %>: %{A%}: ;
      except
          _ %>: %{A%}: %{A%}: ;
      where Vx in ( а о э ө )
            Vy in ( ы у и ү )
      matched ;

"second element of diphthongs and long vowels immediately before {A} round"
Vx:Vy <=> :RoundVow _ %>: %{A%}: ;
      except
          _ %>: %{A%}: %{A%}: ;
      where Vx in ( а о э ө )
            Vy in ( у у ү ү )
      matched ;

!@ ү ө с к э:ү э:ү >:0 {A}:0 р

"second element of diphthongs and long vowels immediately before {I}{A} round"
Vx:Vy <=> :RoundVow _ %>: %{I%}: %{A%}: ;
      where Vx in ( а э )
            Vy in ( у ө )
      matched ;

!@ ү ө с к э:ү э:ө >:0 {I}:0 {A}:0 н

"first element of diphthongs and long vowels immediately before {I}{I}"
Vx:Vy <=> _ :RealVow %>: %{I%}: %{I%}: ;
      where Vx in ( а о э ө )
            Vy in ( ы у и ү )
      matched ;

"second element of diphthongs and long vowels immediately before {I}{I}"
Vx:Vy <=> _ %>: %{I%}: %{I%}: ;
      where Vx in ( а о э ө )
            Vy in ( ы у и ү )
      matched ;

"vowel harmony for {a} except after уо and үө"
%{a%}:Vy <=> :Vx [ :Cons* :Cons ]/[ [ :0 - ImaginaryVow: ] | :ь ]* _ ;
         except
             :ү :ө [ :Cons* :Cons]/:0* _ ;
             :у :о [ :Cons* :Cons]/:0* _ ;
         where Vx in ( а о ы у э е и ө ү я )
               Vy in ( а о а а э э э ө э а )
         matched ;

"vowel harmony for {a} after үө"
%{a%}:э <=> :ү :ө [ :Cons* :Cons ]/[ :0 - ImaginaryVow: ]* _ ;

!FIXME 
"vowel harmony for {I} after back harmony marker"
%{I%}:Vy <= :Vx [ :Cons* :Cons ]/[ :0 - ImaginaryVow: ]* _ ;
            ! [ %{ъ%}: | %{☭%}: ] %>: [ :Cons* ]/:0* _ ;            
         except
            :Vow [ :Cons* :Cons ]/[ :0 - %{☭%}: ]* _ ;
         where Vx in ( а о ы у э е и ө ү я )
               Vy in ( ы у ы у ы ы ы ы ы ы )
         matched ;

"vowel harmony for {i} after back harmony marker"
%{i%}:ы <= :Vow [ :Cons* :Cons ]/[ :0 - ImaginaryVow: ]* _ ;
            ! [ %{ъ%}: | %{☭%}: ] %>: [ :Cons* ]/:0* _ ;            
        except
            :Vow [ :Cons* :Cons ]/[ :0 - %{☭%}: ]* _ ;




!!!== Defaults that shouldn't be necessary ==!!!

"{G} after high vowel"
%{G%}:г <=> [ :HighVow | ImaginaryHighVow: ] [ [ :0 - [ Imaginary: | %{D%}: ] ] | %-: ]* _ ;
            [ [ :HighSonCns | [ л: - :0 ] | %{йр%}: | %{л%}: ] - :Nasal ] [ [ :0 - [ Imaginary: | %{D%}: ] ] | %-: ]*  _ ;

!@ с ы л >:0 {G}:г {A}:а


"vowel harmony for {A} after уо etc."
! shouldn't need a rule because it's the default а, but doesn't seem to work without one
%{A%}:а <=  :у :о [ :Cons* :Cons ]/[ :0 - ImaginaryVow: ]* _ ;
            ! %{ъ%}: %>: [ :Cons* ]/:0* _ ;            
            :Vow [ :Cons* :Cons %{☭%}: ]/[ :0 - [ %{☭%}: | ImaginaryVow: ] ]* _ ;
            :Vow [ :Cons* %{☭%}: :Cons ]/[ :0 - [ %{☭%}: | ImaginaryVow: ] ]* _ ;
            :Vow [ %{☭%}: :Cons* :Cons ]/[ :0 - [ %{☭%}: | ImaginaryVow: ] ]* _ ;
            ! :Cons/[ :0 - %{☭%}: ] %{☭%}: _ ;
            ! %{☭%}: :Cons/[ :0 - %{☭%}: ] _ ;
            ! :Vow %{☭%}: [ :Cons* :Cons ]/[ :0 - %{☭%}: ]* _ ;
               !компьютер{ъ}>{L}{A}р
               !@ Д ь о к у у с к а й >:0 {T}:д {A}:а {A}:а ҕ {I}:ы
            ! :Vow [ :Cons* :Cons ]/[ :0 - %{☭%}: ]* %{☭%}: _ ;
            ! :Vow [ :Cons* %{☭%}: :Cons ]/[ :0 - %{☭%}: ]* _ ;
        except
            :ү :ө [ :Cons* :Cons ]/:0* _ ;
               ! protects against үө forms with %{☭%} (haha!)
            _ %>: %{I%}: [ %{I%}: | %{A%}: ]  ;


"vowel harmony for {A} after үө"
%{A%}:э <=  :ү :ө [ :Cons* :Cons ]/[ :0 - ImaginaryVow: ]* _ ;
        except
!             %{ъ%}: %>: [ :Cons* :Cons ]/:0* _ ; 
            _ %>: %{I%}: [ %{I%}: | %{A%}: ]  ;

!@ б а р >:0 {I}:ы {A}:а н

"vowel harmony for {a} after уо"
! shouldn't need a rule because it's the default а, but doesn't seem to work without one
%{a%}:а <=> :у :о [ :Cons* :Cons ]/:0* _ ;

!"Epenthesis in some stems"
!%{y%}:0 <=> _ [ :Cons :Vow ]/[ :0 | %>: ] ;
!   !@ ис{y}т>{A}{р}

"Delete т after н or с when not at the end of a word"
т:0 <=> [ :н | :с | :к ] _ %>:/[ :0 - %>: ] ;
    except
        .#. у: с: _ ;

"{H} as һ only after vowels"
%{H%}:һ <=> :Vow/:0* _ ;
        except
            [ %{H%}: - :һ ] %{I%}: _ ;
