Alphabet

   А Б В Г Д Е Ё Ж З И Й К Л М Н О Ө П Р С Т У Ү Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я Һ Ҥ Ҕ
   а б в г д е ё ж з и й к л м н о ө п р с т у ү ф х ц ч ш щ ъ ы ь э ю я һ ҥ ҕ

   %{E%}:а %{E%}:э %{E%}:ө %{E%}:о

   !%{A%}:а %{A%}:э %{A%}:ө %{A%}:о
   %{A%}:а
   !%{I%}:и %{I%}:у %{I%}:ү %{I%}:ы
   %{I%}:ы

   %{L%}:л ! %{L%}:т %{L%}:д %{L%}:н
   %{T%}:т ! %{T%}:д %{T%}:л %{T%}:н

   %{B%}:б ! %{B%}:п %{B%}:м

   %{G%}:г ! %{G%}:к %{G%}:ҕ %{G%}:х %{G%}:ҥ

   %{i%}:0 ! %{i%}:и %{i%}:у %{i%}:ү %{i%}:ы

   %{n%}:0 ! %{n%}:н

   %{t%}:0 ! %{t%}:т

   %{a%}:0

   %{р%}:р

   %{ъ%}:0

   %>:0 ; 


Sets

Vow = Я Ю Ё А О Ы У Э Е И Ө Ү
      я ю ё а о ы у э е и ө ү ;

Nasal = М Н Ҥ
        м н ҥ ;

HighSonCns = Й Р
             й р ;

VoicelessCns = П Т К Х С 
               п т к х с ;

Obs = Б В Г Д Ж З К П Р С Т Ф Х Ц Ч Ш Щ Һ Ҕ
      б в г д ж з к п р с т ф х ц ч ш щ һ ҕ ;

Cons = Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц Ч Ш Щ Һ Ҥ Ҕ
       б в г д ж з й к л м н п р с т ф х ц ч ш щ һ ҥ ҕ ;


Rules

!"Vowel deletion"
!%{A%}:0 <=> _ %>: %{I%}: ; 


!!!== regressive assimilation ==!!!

"т assimilation"
т:Cy <=> _ %>: Cx: ;
       where Cx in ( %{B%} %{G%} ) ! %{L%} ) 
             Cy in (   п     к   ) !  т ) 
       matched ;  

"{р} → л before л"
%{р%}:л <=> _ [ :л | [ %{L%}: - :Obs ] ]/:0 ;

"н assimilation"
н:ҥ <=> _ %>: :ҥ ; 


!!!== progressive assimilation ==!!!

"Assimilation after nasals"
Cx:Cy <=> :Nasal/:0 _ ;
      where Cx in ( %{L%} %{T%} %{G%} %{B%} )
            Cy in (   н     н     ҥ     м   )
      matched ;

"Assimilation after high-sonority obstruents"
Cx:Cy <=> :HighSonCns/:0 _ ;
      where Cx in ( %{L%} %{T%} )
            Cy in (   д     д   )
      matched ;

"Assimilation after voiceless consonants"
Cx:Cy <=> :VoicelessCns/:0 _ ;
      where Cx in ( %{L%} %{B%} )
            Cy in (   т     п   )
      matched ;

"{T} → л"
%{T%}:л <=> :л/:0 _ ; 

"{G} after х"
%{G%}:х <=> :х/:0 _ ;

"{G} after voiceless consonants besides х"
%{G%}:к <=> [ :VoicelessCns - :х]/:0 _ ;



!!!== deletion ==!!!

"{р} deletes before obstruents or something"
%{р%}:0 <=> _ :Obs/:0 ;


!!!== epenthesis ==!!!

"т after a vowel"
%{t%}:т <=> :Vow/:0 _ ;

"н after a vowel"
%{n%}:н <=> :Vow/:0 _ ;

!@ и д е я >:0 {n}:н {I}:ы

!!!== lenition ==!!!

"intervocalic lenition"
Cx:Cy <=> _ %>: :Vow/:0* ;
      where Cx in ( х с к п )
            Cy in ( ҕ һ г б )
      matched ;

!!!== Vowel Harmony ==!!!

"vowel harmony for {i}"
%{i%}:Vy <=> :Vx [ :Cons* :Cons ]/:0* _ ;
         except 
             _ %>: %{A%}: ; ! because {i}:0 >:0 {A}: 
         where Vx in ( а о ы у э е и ө ү я )
               Vy in ( ы у ы у и и и ү ү ы )
         matched ;

!@ э:э {T}:т {i}:0 >:0 {A}:э

"vowel harmony for {I}"
%{I%}:Vy <=> :Vx [ :Cons* :Cons ]/:0* _ ;
         where Vx in ( а о ы у э е и ө ү я )
               Vy in ( ы у ы у и и и ү ү ы )
         matched ;

"vowel harmony for {A} except after уо and үө"
%{A%}:Vy <=> :Vx [ :Cons* :Cons ]/:0* _ ;
             :Vx _ ; 
         except
             !:ү :ө [ :Cons* :Cons]/:0* _ ;
             :у :о [ :Cons* :Cons]/:0* _ ;
             %{ъ%}: %>: [ :Cons* ]/:0* _ ; 
         where Vx in ( а о ы у э е и ө ү я )
               Vy in ( а о а а э э э ө э а )
         matched ;

"vowel harmony for {A} after уо"
! shouldn't need a rule because it's the default а, but doesn't seem to work without one
%{A%}:а <=> :у :о [ :Cons* :Cons ]/:0* _ ;
           %{ъ%}: %>: [ :Cons* ]/:0* _ ;            

!компьютер{ъ}>{L}{A}р

!@ Д ь о к у у с к а й >:0 {T}:д {A}:а {A}:а ҕ {I}:ы

!"vowel harmony for {A} after үө"
!%{A%}:э <=> :ү :ө [ :Cons* :Cons ]/:0* _ ;
!         except
!             %{ъ%}: %>: [ :Cons* :Cons ]/:0* _ ; 

"vowel harmony for {a} except after уо and үө"
%{a%}:Vy <=> :Vx [ :Cons* :Cons ]/:0* _ ;
         except
             :ү :ө [ :Cons* :Cons]/:0* _ ;
             :у :о [ :Cons* :Cons]/:0* _ ;
         where Vx in ( а о ы у э е и ө ү )
               Vy in ( а о а а э э э ө э )
         matched ;

"vowel harmony for {a} after уо"
! shouldn't need a rule because it's the default а, but doesn't seem to work without one
%{a%}:а <=> :у :о [ :Cons* :Cons ]/:0* _ ;

"vowel harmony for {a} after үө"
%{a%}:э <=> :ү :ө [ :Cons* :Cons ]/:0* _ ;

